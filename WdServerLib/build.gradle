plugins {
    id 'com.android.library'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

def VERSION_CODE = 10606
def VERSION_NAME = "1.6.6"

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            buildConfigField "String", "VERSION_NAME", "\"" + VERSION_NAME + "\""
            buildConfigField "int", "VERSION_CODE", VERSION_CODE.toString()
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            buildConfigField "String", "VERSION_NAME", "\"" + VERSION_NAME + "\""
            buildConfigField "int", "VERSION_CODE", VERSION_CODE.toString()
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }
}

afterEvaluate {
    publishing {
        publications {
// Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.gregko'
                artifactId = 'Mini-WebDAV-server-for-Android'
                version = "$VERSION_NAME"
            }
        }
    }
}


dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'pub.devrel:easypermissions:3.0.0'
    implementation 'io.milton:milton-api:3.1.1.488'
    implementation ('io.milton:milton-server-ce:3.1.1.488'){
        exclude (group: 'io.milton', module: 'milton-mail-api')
        exclude(group:'io.milton', module:'milton-mail-server')
    }
    implementation 'org.slf4j:slf4j-log4j12:1.6.1'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}